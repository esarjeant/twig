apply plugin: 'java'

description = """Twig JDBC driver for Cassandra/CQL"""

group = 'com.micromux'
version = '3.0.0-SNAPSHOT'

test.outputs.upToDateWhen { false }

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
     maven { url "http://repo.maven.apache.org/maven2" }
}

configurations {
    distribution
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.0'
    compile group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version:'3.1.2'
    compile group: 'com.google.guava', name: 'guava', version:'18.0'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.cassandraunit', name: 'cassandra-unit', version:'3.0.0.1'
    testCompile group: 'io.netty', name: 'netty-all', version: '4.0.27.Final'
    testCompile group: 'net.ju-n.compile-command-annotations', name: 'compile-command-annotations', version: '1.0.3'
}

// custom properties for testing
tasks.withType(Test) {
    systemProperty 'host', System.getProperty('host', 'localhost')
    systemProperty 'port', System.getProperty('port', '19042')
}

// log test activity during execution
test {
    afterTest { desc, result ->
        println "Completed ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

// JAR includes all dependencies
jar {
    manifest {
        attributes "Main-Class": "com.micromux.cassandra.jdbc.CassandraDriver"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}